project(rime-aipara)
cmake_minimum_required(VERSION 3.10)

aux_source_directory(src aipara_src)
aux_source_directory(src/common aipara_common_src)
set(aipara_src ${aipara_src} ${aipara_common_src})

add_library(rime-aipara-objs OBJECT ${aipara_src})
if(BUILD_SHARED_LIBS)
  set_target_properties(rime-aipara-objs
    PROPERTIES
    POSITION_INDEPENDENT_CODE ON)
endif()
target_include_directories(rime-aipara-objs PRIVATE
  ${CMAKE_SOURCE_DIR}/deps/opencc/deps/rapidjson-1.1.0
  ${CMAKE_CURRENT_SOURCE_DIR}/src)

set(ZeroMQ_LIBRARY "")
set(ZeroMQ_INCLUDE_DIR "")
set(ZeroMQ_LIBRARY_IS_TARGET FALSE)

find_package(ZeroMQ QUIET)
if(ZeroMQ_FOUND)
  if(TARGET ZeroMQ::ZeroMQ)
    set(ZeroMQ_LIBRARY ZeroMQ::ZeroMQ)
    set(ZeroMQ_LIBRARY_IS_TARGET TRUE)
  elseif(TARGET libzmq)
    set(ZeroMQ_LIBRARY libzmq)
    set(ZeroMQ_LIBRARY_IS_TARGET TRUE)
  endif()
  if(NOT ZeroMQ_INCLUDE_DIR AND ZeroMQ_INCLUDE_DIRS)
    set(ZeroMQ_INCLUDE_DIR ${ZeroMQ_INCLUDE_DIRS})
  endif()
endif()

if(ZeroMQ_LIBRARY_IS_TARGET)
  get_target_property(_zmq_includes ${ZeroMQ_LIBRARY} INTERFACE_INCLUDE_DIRECTORIES)
  if(_zmq_includes AND NOT ZeroMQ_INCLUDE_DIR)
    set(ZeroMQ_INCLUDE_DIR ${_zmq_includes})
  endif()
endif()

if(NOT ZeroMQ_LIBRARY OR NOT ZeroMQ_INCLUDE_DIR)
  find_package(PkgConfig QUIET)
  if(PKG_CONFIG_FOUND)
    pkg_check_modules(PC_ZMQ QUIET libzmq)
  endif()

  if(PC_ZMQ_FOUND)
    if(NOT ZeroMQ_INCLUDE_DIR)
      set(ZeroMQ_INCLUDE_DIR ${PC_ZMQ_INCLUDE_DIRS})
    endif()
    if(NOT ZeroMQ_LIBRARY)
      find_library(ZeroMQ_LIBRARY NAMES zmq PATHS ${PC_ZMQ_LIBRARY_DIRS})
    endif()
  endif()
endif()

if(NOT ZeroMQ_LIBRARY)
  find_library(ZeroMQ_LIBRARY NAMES zmq)
endif()
if(NOT ZeroMQ_INCLUDE_DIR)
  find_path(ZeroMQ_INCLUDE_DIR NAMES zmq.h)
endif()
if(NOT ZeroMQ_LIBRARY OR (NOT ZeroMQ_INCLUDE_DIR AND NOT ZeroMQ_LIBRARY_IS_TARGET))
  message(FATAL_ERROR "libzmq 未找到，请安装 ZeroMQ。")
endif()

if(ZeroMQ_INCLUDE_DIR)
  target_include_directories(rime-aipara-objs PRIVATE ${ZeroMQ_INCLUDE_DIR})
endif()

set(plugin_name rime-aipara PARENT_SCOPE)
set(plugin_objs $<TARGET_OBJECTS:rime-aipara-objs> PARENT_SCOPE)
set(plugin_deps ${rime_library} ${ZeroMQ_LIBRARY} PARENT_SCOPE)
set(plugin_modules "aipara" PARENT_SCOPE)
